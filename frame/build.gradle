import java.text.SimpleDateFormat

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id "com.vanniktech.maven.publish.base"
}

def version = "2.0.1.23"

android {
    compileSdk 31
    namespace "com.qz.frame"
    defaultConfig {
        minSdk 23
        targetSdk 30
        versionCode 20123
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    // 自定义输出配置
    libraryVariants.configureEach { variant ->
        variant.outputs.configureEach {
            Date nowTime = new Date()
            SimpleDateFormat time = new SimpleDateFormat("YYYYMMdd")
            outputFileName = "QzKtFrame_v${version}_${time.format(nowTime)}.aar"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    api 'androidx.core:core-ktx:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.recyclerview:recyclerview:1.3.0'
    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.8.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // ViewModel
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    // LiveData
    api 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    // Saved state module for ViewModel
    api 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.6.2'
    //fragment快速实现（可选）
    api 'com.geyifeng.immersionbar:immersionbar:3.2.2'
    // kotlin扩展（可选）
    api 'com.geyifeng.immersionbar:immersionbar-ktx:3.2.2'
    //retrofit + okHttp3
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    //dex分包,解决方法数超限(65535)
    api 'com.android.support:multidex:1.0.3'
    //腾讯mmkv
    api 'com.tencent:mmkv-static:1.3.1'
    //导航栏
    api 'com.ashokvarma.android:bottom-navigation-bar:2.2.0'
    //基于LiveData的事件总线
    api 'io.github.jeremyliao:live-event-bus-x:1.8.0'
    //RecyclerViewAdapter
    api 'io.github.cymchad:BaseRecyclerViewAdapterHelper:3.0.14'
    //glide
    api 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    api 'com.github.bumptech.glide:okhttp3-integration:4.16.0'
    api 'jp.wasabeef:glide-transformations:4.0.0'
    //下拉刷新
    api 'io.github.scwang90:refresh-layout-kernel:2.0.6'
    api 'io.github.scwang90:refresh-header-material:2.0.6'
    api 'io.github.scwang90:refresh-footer-classics:2.0.6'
    //屏幕适配
    api 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'
    //日志
    api 'com.orhanobut:logger:2.2.0'
    //权限处理
    api 'com.guolindev.permissionx:permissionx:1.7.1'
    //dataBinding严格模式
    api 'com.kunminx.arch:strict-databinding:5.6.0'
    //shimmer动画
    api 'com.facebook.shimmer:shimmer:0.5.0'
    //worker
    api "androidx.work:work-runtime-ktx:2.8.1"
}
ext {
    PUBLISH_GROUP_ID = "io.gitee.ezhuwx"
    PUBLISH_ARTIFACT_ID = 'QzKtFrame'
    PUBLISH_VERSION = version
}
apply from: "${rootProject.projectDir}/publish-maven.gradle"